/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import org.apache.tools.ant.taskdefs.condition.Os

description = 'Ballerinax - Azure Functions Extension Annotations'

configurations {
    balTools
    zipPartent
    extensionPlugin
}

dependencies {
    balTools("org.ballerinalang:jballerina-tools:${ballerinaLangVersion}") {
        transitive = false
    }
    extensionPlugin project(path: ":azurefunctions-extension")
}

jar {
    enabled = false
}

clean {
    delete "$project.projectDir/output"
    delete "$project.projectDir/target"
    delete "$project.projectDir/Ballerina.lock"
}

def bDistribution = file("$project.buildDir/extracted-distribution/jballerina-tools-${ballerinaLangVersion}")
def extensionPluginArtifact = file("$project.projectDir/../azurefunctions-extension/build/libs/")

task unzip {
    doLast {
        configurations.balTools.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            copy {
                from project.zipTree(artifact.getFile())
                into new File(project.buildDir, "extracted-distribution/")
            }
        }
    }
    outputs.dir bDistribution
}

task unpackStdLibs() {
    doLast {
        configurations.ballerinaStdLibs.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            copy {
                from project.zipTree(artifact.getFile())
                into new File("${buildDir}/extracted-distribution", artifact.name + "-zip")
            }
        }
    }
}

task copyStdlibs(type: Copy) {
    into bDistribution

    /* Standard Libraries */
    configurations.ballerinaStdLibs.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        def artifactExtractedPath = "${buildDir}/extracted-distribution/" + artifact.name + "-zip"
        into("repo/bala") {
            from "${artifactExtractedPath}/bala/"
        }
        into("repo/cache") {
            from "${artifactExtractedPath}/cache"
        }
    }
}

task copyThridPartyDependencies(type: Copy) {
    from configurations.thirdPartyJars
    into "${bDistribution}/bre/lib"
}


def ballerinaToml = file("$project.projectDir/Ballerina.toml")
def semverMatcher = ballerinaToml.getText("UTF-8") =~ /(([0-9]+)\.([0-9]+)\.([0-9]+))/
def tomlSemver = semverMatcher[0][1]

def artifactParent = file("$project.projectDir/build/target/azurefunctions-ballerinax-zip/")
def artifactDocs = file("$project.buildDir/apidocs/docs/")
def artifactBala = file("$project.projectDir/target/bala")
def artifactCache = file("$project.projectDir/target/cache/")
def platform = "any"

task ballerinaBuild {
    dependsOn unzip
    inputs.dir file(project.projectDir)
    doLast {
        def distributionBinPath = project.projectDir.absolutePath + "/build/extracted-distribution/jballerina-tools-${ballerinaLangVersion}/bin"
        exec {
            workingDir project.projectDir
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "$distributionBinPath/bal.bat doc"
            } else {
                commandLine 'sh', '-c', "$distributionBinPath/bal doc"
            }
        }
        copy {
            from file("$project.projectDir/target/apidocs/ballerinax/azure_functions")
            into file("$project.buildDir/apidocs/docs/azure_functions")
        }
        exec {
            workingDir project.projectDir
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "$distributionBinPath/bal.bat build -c"
            } else {
                commandLine 'sh', '-c', "$distributionBinPath/bal build -c"
            }
        }
        // extract bala file to artifact cache directory
        file("$artifactBala").eachFileMatch(~/.*.bala/) { balaFile ->
            copy {
                from zipTree(balaFile)
                into file("$artifactParent/bala/ballerinax/azure_functions/$tomlSemver/$platform")
            }
        }
        copy {
            from file("$artifactDocs")
            into file("$artifactParent/docs")
        }
        copy {
            from file("$extensionPluginArtifact")
            into file("${artifactParent}/libs")
        }
        copy {
            from file("$artifactCache")
            into file("$artifactParent/cache")
        }
    }
    outputs.dir file("$artifactParent")
}

artifacts {
    zipPartent file: artifactParent, builtBy: ballerinaBuild
}

task createArtifactZip(type: Zip) {
    destinationDirectory = file("$buildDir/distributions")
    from ballerinaBuild
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact source: createArtifactZip, extension: 'zip'
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ballerina-platform/module-ballerinax-azure.functions")
            credentials {
                username = System.getenv("packageUser")
                password = System.getenv("packagePAT")
            }
        }
    }
}

unpackStdLibs.dependsOn unzip
copyStdlibs.dependsOn unpackStdLibs
copyThridPartyDependencies.dependsOn copyStdlibs
ballerinaBuild.dependsOn copyThridPartyDependencies
build.dependsOn ballerinaBuild
